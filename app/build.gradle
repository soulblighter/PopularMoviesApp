//apply plugin: "net.ltgt.errorprone"

apply plugin: "com.android.application"

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "br.com.soulblighter.popularmoviesapp"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 5
        versionName "1.${versionCode}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true

        // Please ensure you have a valid API KEY for themoviedb.org to use this app
        // A valid key will need to be entered
        if (project.hasProperty('API_KEY')) {
            buildConfigField("String", "API_KEY", API_KEY)
        } else {
            buildConfigField("String", "API_KEY", "\"\"")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            // Faster build of debug version (only for dev)
            // https://hackernoon.com/how-to-speed-up-your-slow-gradle-builds-5d9a9545f91a
            // Tip 7 — Avoid inadvertent changes
            ext.alwaysUpdateBuildId = false
            versionNameSuffix "dev"
            splits.abi.enable false
            splits.density.enable false
            // Tip 5 — Disable PNG Crunching
            aaptOptions.cruncherEnabled false
            //only package english translations, and xxhdpi resources
            //resConfigs ("en", "xxhdpi")
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "popularmoviesapp-${variant.versionName}.apk"
        }
    }

    dataBinding.enabled true
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })

    // Google support
    def support_version = "27.1.1"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"

    // Archteture components
    def archteture_version = "1.1.1"
    implementation "android.arch.persistence.room:runtime:$archteture_version"
    annotationProcessor "android.arch.persistence.room:compiler:$archteture_version"
    androidTestImplementation "android.arch.persistence.room:testing:$archteture_version"
    // RxJava support for Room
    implementation "android.arch.persistence.room:rxjava2:$archteture_version"

    // Lifecycle components
    implementation "android.arch.lifecycle:extensions:$archteture_version"
    annotationProcessor "android.arch.lifecycle:compiler:$archteture_version"

    // Image
    implementation "com.squareup.picasso:picasso:2.5.2"

    // JSON
    implementation "com.google.code.gson:gson:2.8.0"

    // Web request - OkHttp / Retrofit
    implementation "com.squareup.okhttp3:okhttp:3.10.0"
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.8.0"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "io.reactivex.rxjava2:rxjava:2.1.12"

    // Dagger
    def dagger_version = "2.16"
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    //implementation "com.google.dagger:dagger-android:$dagger_version"
    //implementation "com.google.dagger:dagger-android-support:$dagger_version" // if you use the support libraries
    //annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation "junit:junit:4.12"
}
