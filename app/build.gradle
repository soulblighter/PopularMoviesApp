apply plugin: "com.android.application"

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "br.com.soulblighter.popularmoviesapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 6
        versionName "1.${versionCode}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true

        // Please ensure you have a valid API KEY for themoviedb.org to use this app
        // A valid key will need to be entered
        if (project.hasProperty('API_KEY')) {
            buildConfigField("String", "API_KEY", API_KEY)
        } else {
            buildConfigField("String", "API_KEY", "\"\"")
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            // Faster build of debug version (only for dev)
            // https://hackernoon.com/how-to-speed-up-your-slow-gradle-builds-5d9a9545f91a
            // Tip 7 — Avoid inadvertent changes
            ext.alwaysUpdateBuildId = false
            versionNameSuffix "dev"
            splits.abi.enable false
            splits.density.enable false
            // Tip 5 — Disable PNG Crunching
            aaptOptions.cruncherEnabled false
            //only package english translations, and xxhdpi resources
            //resConfigs ("en", "xxhdpi")
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "popularmoviesapp-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    androidTestImplementation("androidx.test.espresso:espresso-core:3.1.0", {
        exclude group: "com.android.support", module: "support-annotations"
    })

    // Google support
    def support_version = "1.0.0"
    implementation "androidx.legacy:legacy-support-v4:$support_version"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:$support_version"
    implementation "com.google.android.material:material:$support_version"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    // Archteture components
    def archteture_version = "2.0.0"
    implementation "androidx.room:room-runtime:$archteture_version"
    annotationProcessor "androidx.room:room-compiler:$archteture_version"
    androidTestImplementation "androidx.room:room-testing:$archteture_version"
    // RxJava support for Room
    implementation "androidx.room:room-rxjava2:$archteture_version"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:$archteture_version"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$archteture_version"

    // Image
    implementation "com.squareup.picasso:picasso:2.5.2"

    // JSON
    implementation "com.google.code.gson:gson:2.8.2"

    // Web request - OkHttp / Retrofit
    implementation "com.squareup.okhttp3:okhttp:3.10.0"
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.8.0"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "io.reactivex.rxjava2:rxjava:2.1.12"

    // Dagger
    def dagger_version = "2.20"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation "junit:junit:4.12"
}
